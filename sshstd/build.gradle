
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka-android'
    id 'com.github.dcendents.android-maven'
    id 'com.jfrog.bintray'
}
// версия библиотеки
def libVersion = "0.8.1"
// ссылка на сайт размещения проекта
def siteUrl = 'https://github.com/ostrovskal/sshSTD'
// ссылка на сайт контроля версий
def gitUrl = 'https://github.com/ostrovskal/sshSTD.git'

android {
    sourceSets { main.java.srcDirs += 'src/main/kotlin' }
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 10
        versionName libVersion
        resValue "string", "app_name", "sshSTD"
    }
    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

repositories {
    mavenCentral()
}

group = "com.github.ostrovskal"
version = libVersion

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'com.github.ostrovskal:sshSTD'
                description = 'A small library containing a wrapper for the classes of Android framework'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'serg'
                        name 'Shatalov Sergey'
                        email 'ostrov_skal@mail.ru'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    noStdlibLink =  true
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

artifacts {
    archives dokkaJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = 'sshSTD'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = libVersion
            desc = 'A small library containing a wrapper for the classes of Android framework'
            vcsTag = libVersion
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
